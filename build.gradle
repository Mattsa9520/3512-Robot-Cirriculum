plugins {
    id "cpp"
    id "google-test-test-suite"
    id "edu.wpi.first.GradleRIO" version "2021.1.1-beta-2"
    id "org.ysb33r.doxygen" version "0.5"
}

// For testing development versions of WPILib
wpi.wpilibVersion = '2021.1.1-beta-2-12-g5cd2ad1'

// For automatically using the newest development version of WPILib
// wpi.wpilibVersion = '2020.+'

// For using remote development artifacts
wpi.maven.useDevelopment = true

// For using local development artifacts
// wpi.maven.useFrcMavenLocalDevelopment = true

// Define my targets (RoboRIO) and artifacts (deployable files)
// This is added by GradleRIO's backing project EmbeddedTools.
deploy {
    targets {
        roboRIO("roborio") {
            // Team number is loaded either from the .wpilib/wpilib_preferences.json
            // or from command line. If not found an exception will be thrown.
            // You can use getTeamOrDefault(team) instead of getTeamNumber if you
            // want to store a team number in this file.
            team = frc.getTeamNumber()
        }
    }
    artifacts {
        frcNativeArtifact('frcCpp') {
            targets << "roborio"
            component = 'frcUserProgram'
            // Debug can be overridden by command line, for use with VSCode
            debug = frc.getDebugOrDefault(false)
        }
        // Built in artifact to deploy arbitrary files to the roboRIO.
        fileTreeArtifact('frcStaticFileDeploy') {
            // The directory below is the local directory to deploy
            files = fileTree(dir: 'src/main/deploy')
            // Deploy to RoboRIO target, into /home/lvuser/deploy
            targets << "roborio"
            directory = '/home/lvuser/deploy'
        }
    }
}

// Set this to true to enable desktop support.
def includeDesktopSupport = true

// Enable simulation gui support. Must check the box in vscode to enable support
// upon debugging
dependencies {
    simulation wpi.deps.sim.gui(wpi.platforms.desktop, true)
    simulation wpi.deps.sim.driverstation(wpi.platforms.desktop, true)

    // Websocket extensions require additional configuration.
    // simulation wpi.deps.sim.ws_server(wpi.platforms.desktop, true)
    // simulation wpi.deps.sim.ws_client(wpi.platforms.desktop, true)
}

nativeUtils.platformConfigs.named("linuxx86-64").configure {
    it.linker.args.add('-lstdc++fs')
}

if (project.hasProperty("asan")) {
    nativeUtils.platformConfigs.named("linuxx86-64").configure {
        it.cppCompiler.args.add('-g3')
        it.cppCompiler.args.add('-fsanitize=address')
        it.linker.args.add('-fsanitize=address')
    }
}

model {
    components {
        frcUserProgram(NativeExecutableSpec) {
            targetPlatform wpi.platforms.roborio
            if (includeDesktopSupport) {
                targetPlatform wpi.platforms.desktop
            }

            binaries {
              all {
                if (it.buildType.name.contains('debug')) {
                  it.buildable = false
                }
              }
            }

            sources.cpp {
                source {
                    srcDir 'src/main/cpp'
                    include '**/*.cpp', '**/*.cc'
                }
                exportedHeaders {
                    srcDir 'src/main/include'
                }
            }

            // Defining my dependencies. In this case, WPILib (+ friends), and vendor libraries.
            wpi.deps.vendor.cpp(it)
            wpi.deps.wpilib(it)
        }
    }
    testSuites {
        frcUserProgramTest(GoogleTestTestSuiteSpec) {
            testing $.components.frcUserProgram

            binaries {
              all {
                if (it.buildType.name.contains('debug')) {
                  it.buildable = false
                }
              }
            }

            sources.cpp {
                source {
                    srcDir 'src/test/cpp'
                    include '**/*.cpp'
                }

                exportedHeaders {
                    srcDir 'src/test/include'
                }
            }

            wpi.deps.vendor.cpp(it)
            wpi.deps.wpilib(it)
            wpi.deps.googleTest(it)
        }
    }
}

task buildAthena {
    dependsOn 'frcUserProgramLinuxathenaReleaseExecutable'
}

task removeLogs(type: Exec) {
    workingDir '.'
    commandLine './buildscripts/remove-logs.sh'
}

task test {
    dependsOn 'testRelease'
}

task testDebug {
    dependsOn 'removeLogs'
    dependsOn 'runFrcUserProgramTest' + wpi.platforms.desktop.capitalize() + 'DebugGoogleTestExe'
}

task testRelease {
    dependsOn 'removeLogs'
    dependsOn 'runFrcUserProgramTest' + wpi.platforms.desktop.capitalize() + 'ReleaseGoogleTestExe'
}

task simulate(type: Exec) {
    dependsOn 'removeLogs'
    dependsOn 'simulateFrcUserProgram' + wpi.platforms.desktop.capitalize() + 'DebugExecutable'
    workingDir 'build/stdout'
    commandLine 'tail', '-f', 'simulateFrcUserProgram' + wpi.platforms.desktop.capitalize() + 'DebugExecutable.log'
}

task valgrind(type: Exec) {
    workingDir '.'
    commandLine './buildscripts/valgrind.sh'
}

doxygen {
    executables {
        doxygen version : '1.8.18',
            baseURI : 'https://frcmaven.wpi.edu/artifactory/generic-release-mirror/doxygen'
    }

    generate_html true
    extension_mapping 'inc=C++'
    project_name 'Robot-2020'
    quiet true
    warn_if_undocumented false
    generate_latex false
    use_mathjax true
    html_timestamp true
    generate_treeview true

    source 'src/main/include'
    outputDir 'docs/'
}
